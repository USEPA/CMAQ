#! /bin/csh -f

# ===================== SITECMP_DAILYO3_v5.2 Run Script =============
# Usage: run.sitecmp_dailyo3 >&! sitecmp_dailyo3_V52.log &
#
# To report problems or request help with this script/program:
#             foley.kristen@epa.gov
# ===================================================================

#> Source the config.cmaq file to set the run environment
 source ../../../config.cmaq

#> Set the working directory
 set BASE  = $cwd
 set APPL = v52b

 cd $BASE; date; set timestamp; echo " "; set echo

#> define obs species 
 setenv OBS_SPECIES OZONE

#> define model species
 setenv OZONE "1*O3,ppb"

#> Ozone convert factor (ppm to ppb)
 setenv OBS_FACTOR "1"        # Multiply by 1000 to convert ppm to ppb

#> define time window
 setenv START_DATE 2011182
 setenv END_DATE   2011213  
 setenv START_TIME 0      
 setenv END_TIME   230000   

#> Number of 8hr values to use when computing daily maximum 8hr ozone.
#> Allowed values are 24 (use all 8-hr averages with starting hours 
#> from 0 - 23 hr local time) and 17 (use only the 17 8-hr averages
#> with starting hours from 7 - 23 hr local time)
 setenv HOURS_8HRMAX 24
# setenv HOURS_8HRMAX 17

#> Start and end hours for partial days. (do not use for full day calculations)
#  setenv PARTIAL_DAY "10,17" 

#> adjust for daylight savings
 setenv APPLY_DLS N

#> Number of hours to add when retrieving time steps from M3_FILE_n files during processing.
#> This should only be non-zero if the M3_FILE_n files were pre-processed with a utility like m3tshift (default 0).
 setenv TIME_SHIFT 0

#> indicate whether or not to check QA flag
 setenv QA_FLAG_CHECK Y 
 setenv QA_FLAG_HEADER "OZONE_F" 
 setenv QA_FLAG_VALUES "BCDFIMP" 

#> set missing value string
 setenv MISSING '-999'

#> Projection sphere type (use type 20 to match WRF/CMAQ)
 setenv IOAPI_ISPH 20

#> Indicates whether the Lambert x/y information should be included in the output file
 setenv LAMBXY N


#############################################################
#  Input files
#############################################################

#> ioapi input files containing VNAMES (max of 10)
 setenv M3_FILE_1 $CMAQ_DATA/cctm/CCTM_v52b_Linux2_x86_64pgi.ACONC.BENCHMARK_20110701

#> SITE FILE containing site-id, longitude, latitude, time zone (tab delimited)
 setenv SITE_FILE  ../obs_data/site_files/CASTNET_sites.txt

#> input table containing site-id, time-period, and data fields
 setenv IN_TABLE  ../obs_data/2011/CASTNET_hourly_data_2011.csv

#############################################################
#  Output files
#############################################################

#> output table (comma delimited text file importable to Excel)
 setenv OUT_TABLE CASTNET_Daily_CMAQ_v52.csv

#> Executable call:
   /usr/bin/time $BASE/BLD_$APPL/SITECMP_DAILYO3_${APPL}_$EXEC_ID
 
 date
 exit()

#! /bin/csh -f

 set echo
 set BASE  = $cwd

#> option to set compiler and build a new executable (not required)
 setenv COMPILER  GFORT #> INTEL, PGF90, or GFORT
 set compile = "F"    #> Compile the program? T or F

#> CMAQv5.1 Mechanism Options: cb05tucl_ae6_aq cb05tump_ae6_aq cb05e51_ae6_aq saprc07tb_ae6_aq saprc07tc_ae6_aq  saprc07tic_ae6i_aq   racm2_ae6_aq 
 set Mechanism = racm2_ae6_aq          #> CMAQ mechanism ID

 set Xpath = ${BASE}/src               #> Executable directory
 set EXEC  = CHEMMECH                  #> Executable name
 set Opath = ${BASE}"/output/"${Mechanism}   #> Output Directory
 set Mpath = ${BASE}/input/${Mechanism}  #> Directory with mechanism definitions
 
#> option use CMAQ species namelists to determine CGRID species indices
 setenv USE_SPCS_NAMELISTS T

#> compile the program 
 if( ${compile} == "T" )then
   cd ${Xpath}; make clean; make
   if( ! ( -e ${EXEC} ) )then
      echo "failed to compile ${Xpath}/${EXEC}"
      exit()
   endif
   cd ${BASE}
 endif

 cd ${Mpath} ; set MCFL =  ` \ls mech*def ` ; cd ${BASE}

 if( !( -e $Mpath/$MCFL ) )then
   \ls $Mpath/$MCFL
    exit()
 endif

 set echo
 setenv MECHDEF  $Mpath/$MCFL
 setenv MAPPING_ROUTINE "${Xpath}/map_chemistry_spc.F90"
 
 set NML    = ${Mpath}
 set NML_TR = ${BASE}/input/trac0
 set GC_NML = $NML/GC_${Mechanism}.nml
 set AE_NML = $NML/AE_${Mechanism}.nml
 set NR_NML = $NML/NR_${Mechanism}.nml

 set TR_NML = $NML_TR/Species_Table_TR_0.nml

 setenv gc_matrix_nml $GC_NML
 setenv ae_matrix_nml $AE_NML
 setenv nr_matrix_nml $NR_NML
 setenv tr_matrix_nml $TR_NML

# output files ................................................................
 
 if( ! ( -d $Opath ) ) mkdir -p $Opath

 if( $Opath != ${Mpath} )then
    \cp -f $MECHDEF  $Opath/.
    \cp -f $NML/*nml $Opath/.
 endif

 setenv SPCSDATX    $Opath/SPCS.ext # lists species in mechanism

 setenv RXNS_DATA_MODULE $Opath/RXNS_DATA_MODULE.F90
 setenv RXNS_FUNC_MODULE $Opath/RXNS_FUNC_MODULE.F90
 setenv OUTDIR           $Opath

 set KPP_Out_Path = ${Opath}
 if( ! ( -d ${KPP_Out_Path} ) )mkdir -p ${KPP_Out_Path}

 set KPP_EQN =  mech_${Mechanism}.eqn
 set KPP_SPC =  mech_${Mechanism}.spc

 setenv EQNS_KPP_FILE ${KPP_Out_Path}/${KPP_EQN}
 setenv SPCS_KPP_FILE ${KPP_Out_Path}/${KPP_SPC} 

 if( !( -e $Xpath/$EXEC ) )then
  ls -l $Xpath/$EXEC
  exit()
 endif

 $Xpath/$EXEC

 exit()

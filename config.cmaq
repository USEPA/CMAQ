#!/bin/csh -f

# ================== CMAQ5.2 Configuration Script =================== #
# Requirements: I/O API & netCDF libraries                            #
#               PGI, Intel, or Gnu Fortran compiler                   #
#               MPICH for multiprocessor computing                    #
# Optional:     Git for GitHub source code repository                 #
#                                                                     #
# Note that this script was configured/tested on Red Hat Linux O/S    #
#                                                                     #
# To report problems or request help with this script/program:        #
#             http://www.cmascenter.org/help_desk.cfm                 #
# =================================================================== #

#> model source code repository location
 setenv CMAQ_HOME  [Add CMAQv5.2 Install Directory Here] 
 setenv CMAQ_MODEL ${CMAQ_HOME}
 setenv CMAQ_DATA  ${CMAQ_HOME}/data

#===============================================================================
#> architecture & compiler specific settings
#-------------------------------------------------------------------------------

#> Set the compiler
 setenv compiler pgi  #> options: intel, pgi, gcc

#> I/O API, netCDF, and MPI libraries 
 setenv IOAPI /apps/ioapi_31/Linux2_x86pg_pgcc_nomp #> I/O API directory path
 setenv NETCDF /apps/netcdf/netcdf-4.1.1/Linux86-64-pgf90-gcc #> netCDF directory path
 setenv MPI /usr/mpi/pgi/openmpi-1.4.2 #> MPI directory path
 setenv netcdf_lib "-lnetcdf"  #> -lnetcdff -lnetcdf for netCDF v4.2.0 and later
 setenv ioapi_lib "-lioapi"
 setenv pnetcdf_lib "-lpnetcdf"
 setenv mpi_lib "-lmpi"   #> -lmpich for mvapich or -lmpi for openmpi

#> Compiler flags and settings
 switch ( $compiler )
    case intel:
#> Intel fortran compiler......................................................
       setenv myFC mpif90 
       setenv myCC icc
       setenv myFSTD "-O3"
       setenv myDBG  "-g -check bounds -check uninit -fpe0 -fno-alias -ftrapuv -traceback"
       setenv myLINK_FLAG "-openmp"
       setenv myFFLAGS "-fixed -132"
       setenv myFRFLAGS "-free"
       setenv myCFLAGS "-O2"
       setenv extra_lib "-lcurl"
       breaksw

    case pgi:
#> Portland Group fortran compiler.............................................
       setenv myFC mpif90 
       setenv myCC pgcc
       setenv myLINK_FLAG ""
       setenv myFSTD "-Mextend"
       setenv myDBG  "-g -Mbounds -Mchkptr -traceback -Ktrap=fp"
       setenv myFFLAGS "-Mfixed -O3"
       setenv myFRFLAGS "-Mfree -O3"
       setenv myCFLAGS "-O2"
       setenv extra_lib "-lcurl"
       breaksw

    case gcc:
#> gfortran compiler......................................................
       setenv myFC mpif90
       setenv myCC gcc
       setenv myFSTD "-O3 -funroll-loops -finit-character=32"
       setenv myDBG  " -Wall -O0 -g $DBG1 $DBG2"
       setenv myFFLAGS "-ffixed-form -ffixed-line-length-132 -O3 -funroll-loops -finit-character=32"
       setenv myFRFLAGS "-ffree-form -ffree-line-length-none -O3 -funroll-loops -finit-character=32"
       setenv myCFLAGS "-O2"
       setenv myLINK_FLAG ""
       setenv extra_lib "-lgomp"
       breaksw

    default:
       echo "*** Compiler $compiler not found"
       exit(2)
       breaksw

 endsw
 
#===============================================================================
 
 setenv system "`/bin/uname -i`"
 setenv bld_os "`/bin/uname -s``/bin/uname -r | cut -d. -f1`"
 setenv lib_basedir $CMAQ_HOME/lib

#> generate library locations
 setenv CMAQ_LIB ${lib_basedir}/${system}/${compiler}
 setenv MPI_INCL $CMAQ_LIB/mpi/include
 setenv NETCDF_DIR $CMAQ_LIB/netcdf
 setenv PNETCDF_DIR $CMAQ_LIB/pnetcdf
 setenv IOAPI_DIR $CMAQ_LIB/ioapi

 if ( ! -d $CMAQ_LIB ) mkdir -p $CMAQ_LIB
 if ( ! -d $CMAQ_DATA) mkdir -p $CMAQ_DATA
 if ( ! -d $CMAQ_LIB/mpi) ln -s $MPI $CMAQ_LIB/mpi
 if ( ! -d $NETCDF_DIR ) ln -s $NETCDF $NETCDF_DIR
 if ( ! -d $IOAPI_DIR ) then 
    mkdir $IOAPI_DIR
    ln -s $IOAPI $IOAPI_DIR/lib
    ln -s $IOAPI $IOAPI_DIR/include
    ln -s $IOAPI/../ioapi/fixed_src $IOAPI_DIR/src
 endif

#> check for netcdf and I/O API libs/includes, error if they don't exist

if ( ! -e $NETCDF_DIR/lib/libnetcdf.a ) then 
   echo "ERROR: $NETCDF_DIR/lib/libnetcdf.a does not exist in your CMAQ_LIB directory!!! Check your installation before proceeding with CMAQ build."
   exit
endif
if ( ! -e $NETCDF_DIR/include/netcdf.h ) then 
   echo "ERROR: $NETCDF_DIR/include/netcdf.h does not exist in your CMAQ_LIB directory !!! Check your installation before proceeding with CMAQ build."
   exit
endif
if ( ! -e $IOAPI_DIR/lib/libioapi.a ) then 
   echo "ERROR: $NETCDF_DIR/lib/libioapi.a does not exist in your CMAQ_LIB directory!!! Check your installation before proceeding with CMAQ build."
   exit
endif
if ( ! -e $IOAPI_DIR/include/m3utilio.mod ) then 
   echo "ERROR: $NETCDF_DIR/include/m3utilio.mod does not exist in your CMAQ_LIB directory!!! Check your installation before proceeding with CMAQ build."
   exit
endif

#> set executable id
 setenv EXEC_ID ${bld_os}_${system}${compiler}

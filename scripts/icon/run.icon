#! /bin/csh -f

# ====================== ICONv5.0.1 Run Script ======================= #
# Usage: run.icon >&! icon_V5.log &                                    #
# The following environment variable must be set in the external       #
# environment for this script to execute properly:                     #
#   setenv M3DATA = data path for input and output                     #
# The following environment variables must be set internally:          # 
#   setenv GRIDDESC = horizontal projection and grid description table #
#   setenv GRID_NAME = grid definition in GRIDDESC                     #
#   setenv LAYER_FILE = vertical layer definition file                 #
# To report problems or request help with this script/program:         #
#             http://www.cmascenter.org/html/help.html                 #
# ==================================================================== #

#> Source the config.cmaq file to set the run environment
 source ../config.cmaq

#> Check that M3DATA is set:
 if ( ! -e $M3DATA ) then
    echo "   $M3DATA path does not exist"
    exit 1
 endif
 echo " "; echo " Input data path, M3DATA set to $M3DATA"; echo " "

 set APPL     = V5g
 set CFG      = CMAQ-BENCHMARK
 set MECH     = cb05tucl_ae6_aq
 set EXEC     = ICON_${APPL}_$EXECID

#> Set the working directory:
 set BASE     = $cwd
 set BLD      = ${BASE}/BLD_$APPL

 cd $BASE; date; set timestamp; cat $BASE/cfg.${APPL}; echo " "; set echo

#> horizontal grid defn; check GRIDDESC file for GRID_NAME options
 setenv GRIDDESC $M3HOME/scripts/GRIDDESC
 setenv GRID_NAME CMAQ-BENCHMARK
 setenv IOAPI_ISPH 19

#> support needed for large timestep records (>2GB/timestep record) [ NO ]
 setenv IOAPI_OFFSET_64 NO

#> define the model execution id
 setenv EXECUTION_ID $EXEC

#> vertical layer defn
 setenv LAYER_FILE $M3DATA/mcip/METCRO3D_Benchmark

#> species defn
 setenv gc_matrix_nml ${BLD}/GC_$MECH.nml
 setenv ae_matrix_nml ${BLD}/AE_$MECH.nml
 setenv nr_matrix_nml ${BLD}/NR_$MECH.nml
 setenv tr_matrix_nml ${BLD}/Species_Table_TR_0.nml

#> set the path for the output file containing the IC's
 set OUTDIR = $M3DATA/icon
 if ( ! -d "$OUTDIR" ) mkdir -p $OUTDIR

##############################################################################
# This script to run the ICON preprocessor has two major sections;           #
# 1) use default profile inputs, or 2) use CMAQ CTM concentration files.     #
# Follow these steps:                                                        #
#   1) Set IC equal to "profile" or "m3conc"                                 #
#   2) set the remainder of the environment variables for the section being  #
#      used (see below)                                                      #
##############################################################################

 set IC = profile      # use default profile file
#set IC = m3conc       # use CMAQ CTM concentration files (nested runs)

#> date or profile
 if ( $IC == profile ) then
    setenv INIT_CONC_1    "$OUTDIR/ICON_${APPL}_${CFG}_profile -v"     
 endif
 if ( $IC == m3conc ) then 
    set DATE = 2006213  # August 1, 2006                                       
    setenv INIT_CONC_1    "$OUTDIR/ICON_${APPL}_${CFG}_${DATE} -v"     
 endif  

##############################################################################
# Default profile environment variables                                      #
#----------------------------------------------------------------------------#
# IC_PROFILE     = the file containing the default IC profiles               #
#                                                                            #
# NOTE: SDATE and STIME do not need to be set                                #
##############################################################################

 if ( $IC == profile ) then
    setenv IC_PROFILE      $BLD/ic_profile_CB05.dat
 endif
 
##############################################################################
# CMAQ CTM concentration file environment variables (e.g., for nesting )     #
#----------------------------------------------------------------------------#
# CTM_CONC_1     = the CTM concentration file for the coarse domain          #
#                                                                            #
# MET_CRO_3D_CRS = the MET_CRO_3D met file for the coarse domain - must be   #
#                  set if PinG is included or if the vertical grid type is   #
#                  changed between nests                                     #
#                                                                            #
# MET_CRO_3D_FIN = the MET_CRO_3D met file for the inner, nested, domain -   #
#                  must be set if the vertical grid type is changed between  #
#                  nests                                                     #
#                                                                            #
# NOTE: SDATE (yyyyddd) and STIME (hhmmss) must always be set                #
##############################################################################
 
 if ( $IC == m3conc ) then 

    setenv CTM_CONC_1 $M3DATA/cctm/CCTM_d1bCONC.d1b
    setenv MET_CRO_3D_CRS
    setenv MET_CRO_3D_FIN

    setenv SDATE           ${DATE}
    setenv STIME           000000

 endif

#> Executable call:
 time $BASE/$EXEC

 exit() 

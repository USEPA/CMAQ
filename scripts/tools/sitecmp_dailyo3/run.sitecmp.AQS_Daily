#! /bin/csh -f

# ===================== SITECMP_DAILYO3_v5.1 Run Script =============
# Usage: run.sitecmp_dailyo3 >&! sitecmp_dailyo3_V51.log &
#
# To report problems or request help with this script/program:
#             http://www.cmascenter.org
# ===================================================================

#> Source the config.cmaq file to set the run environment
 source ../../config.cmaq

#> Check that M3DATA is set: 
 if ( ! -e $M3DATA ) then
    echo "   $M3DATA path does not exist"
    exit 1
    endif
 echo " "; echo " Input data path, M3DATA set to $M3DATA"; echo " "

 set APPL = D51a
 set EXEC = SITECMP_DAILYO3_${APPL}_$EXEC_ID

#> Set the working directory
 set BASE  = $cwd      
#set BASE  = $M3HOME/scripts/sitecmp
 set BLD   = ${BASE}/BLD_$APPL

 cd $BASE; date; set timestamp; cat $BASE/cfg.${APPL}; echo " "; set echo

#> define obs species 
 setenv OBS_SPECIES O3

#> define model species
 setenv OZONE "1*O3,ppb"

#> Ozone convert factor (ppm to ppb)
 setenv OBS_FACTOR "1"    # Multiply by 1000 to convert ppm to ppb

#> define time window
 setenv START_DATE 2011182
 setenv END_DATE   2011213  
 setenv START_TIME 0      
 setenv END_TIME   230000   

#> Start and end hours for partial days. (do not use for full day calculations)
#  setenv PARTIAL_DAY "10,17" 

#> adjust for daylight savings
 setenv APPLY_DLS N

#> Number of hours to add when retrieving time steps from M3_FILE_n files during processing.
#> This should only be non-zero if the M3_FILE_n files were pre-processed with a utility like m3tshift (default 0).
 setenv TIME_SHIFT 0

#> indicate whether or not to check QA flag 
 setenv QA_FLAG_CHECK N  

#> set missing value string
 setenv MISSING '-999'

#> Projection sphere type (use type 20 to match WRF/CMAQ)
 setenv IOAPI_ISPH 20

#> Indicates whether the Lambert x/y information should be included in the output file
 setenv LAMBXY N


#############################################################
#  Input files
#############################################################

#> ioapi input files containing VNAMES (max of 10)
 setenv M3_FILE_1 $M3DATA/cctm/CCTM_${APPL}_Linux2_x86_64intel_COMBINE_ACONC.CMAQ51-BENCHMARK_20110701

#> SITE FILE containing site-id, longitude, latitude, time zone (tab delimited)
 setenv SITE_FILE $M3DATA/obs_data/site_files/AQS_sites.txt

#> input table containing site-id, time-period, and data fields
 setenv IN_TABLE $M3DATA/obs_data/2011/AQS_hourly_data_2011.csv

#############################################################
#  Output files
#############################################################

#> output table (comma delimited text file importable to Excel)
 setenv OUT_TABLE AQS_Daily_CMAQ_v51.csv

#> Executable call:
   /usr/bin/time $BLD/$EXEC
 
 date
 exit()
